; Comment this line for booting from IDE
LoadModule DEVS:ide.device DEVS:bootide.device 
;LoadModule DEVS:ide.device
skip config-boot

FailAt 7
AddHD
; Check if kick is below version 37
version exec.library >NIL: 37
if warn
  ;Kickstart<3.1
  IF NOT EXISTS WB1.3:
    ; Hard disk not present
    skip config-boot
  ENDIF

  cd WB1.3:
  skip system-redirect
ENDIF

;Check if kick is below version 40
version exec.library >NIL: 40
if warn
  ;Kickstart=2.x
  
  ; Adopt your Partitionname here !
  IF NOT EXISTS WB2.1:
    ;Hard disk not present
    skip config-boot
  ENDIF

  cd WB2.1:
  skip system-redirect
ELSE
  ;Kickstart=3.1
  
  IF NOT EXISTS WB3.1:
    ;Hard disk not present
    skip config-boot
  ENDIF

  cd WB3.1:
  skip system-redirect
endif

lab system-redirect

c/assign c: c
assign sys: ""
assign devs: devs
assign libs: libs
assign l: l
assign fonts: fonts

assign rexx: s
;Workbench 1.3 does not have env-archive by default so create it
IF NOT EXISTS prefs/env-archive
  makedir prefs/env-archive
ENDIF
assign envarc: prefs/env-archive
assign s: s
path reset
failat 22
execute s/startup-sequence
endcli

lab config-boot
failat 22
Setpatch >NIL:
BindDrivers >NIL:

SYS:System/SetMap d
path ram: c: s: add
loadwb delay
endcli

